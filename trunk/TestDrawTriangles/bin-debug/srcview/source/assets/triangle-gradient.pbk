<languageVersion : 1.0;>

kernel NewFilter
<   namespace : "3D pixel shaders";
    vendor : "Illuminati";
    version : 1;
    description : "Shader for multicolor vertex triangle object for flash; use as shader fill";
>
{
    output pixel4 dst;
    parameter float2 pa <
        minValue:float2(-65535,-65535);
        maxValue:float2(65535, 65535);
        defaultValue:float2(0,0);
    >;
    parameter float2 pb <
        minValue:float2(-65535,-65535);
        maxValue:float2(65535, 65535);
        defaultValue:float2(0,0);
    >;
    parameter float2 pc <
        minValue:float2(-65535,-65535);
        maxValue:float2(65535, 65535);
        defaultValue:float2(0,0);
    >;
    parameter float4 ca <
        minValue:float4(0,0,0,0);
        maxValue:float4(1,1,1,1);
        defaultValue:float4(0,0,0,0);
    >;
    parameter float4 cb <
        minValue:float4(0,0,0,0);
        maxValue:float4(1,1,1,1);
        defaultValue:float4(0,0,0,0);
    >;
    parameter float4 cc <
        minValue:float4(0,0,0,0);
        maxValue:float4(1,1,1,1);
        defaultValue:float4(0,0,0,0);
    >;

    void
    evaluatePixel()
    {
        float2 o = outCoord();
        float sa = distance(pb, pc);
        float sb = distance(pa, pc);
        float sc = distance(pa, pb);
        float da = distance(o, pa);
        float db = distance(o, pb);
        float dc = distance(o, pc);
        float pp = (sa+sb+sc)/float(2);
        float ppa = (dc+db+sa)/float(2);
        float ppb = (dc+da+sb)/float(2);
        float ppc = (da+db+sc)/float(2);
        float sABC = sqrt(pp*(pp-sa)*(pp-sb)*(pp-sc));
        float sOBC = sqrt(ppa*(ppa-sa)*(ppa-db)*(ppa-dc));
        float sOAC = sqrt(ppb*(ppb-sb)*(ppb-da)*(ppb-dc));
        float sOAB = sqrt(ppc*(ppc-sc)*(ppc-da)*(ppc-db));
        float aA = sOBC/sABC;
        float aB = sOAC/sABC;
        float aC = sOAB/sABC;
        dst.r = (aA*ca.r)+(aB*cb.r)+(aC*cc.r);
        dst.g = (aA*ca.g)+(aB*cb.g)+(aC*cc.g);
        dst.b = (aA*ca.b)+(aB*cb.b)+(aC*cc.b);
        float tmpAlpha = (aA*ca.a)+(aB*cb.a)+(aC*cc.a);
        float oda = (float(2)*sOBC)/sa;
        float odb = (float(2)*sOAC)/sb;
        float odc = (float(2)*sOAB)/sc;
        if((oda>float(1))&&(odb>float(1))&&(odc>float(1))) {
            dst.a = tmpAlpha;
        } else {
            float minSide = float(0);
            if(oda<odb) {
                if(oda<odc) {
                    minSide = oda;
                } else {
                    if(odc<odb) {
                        minSide = odc;
                    } else {
                        minSide = odb;
                    }
                }
            } else {
                if(odb<odc) {
                    minSide = odb;
                } else {
                    if(oda<odc) {
                        minSide = oda;
                    } else {
                        minSide = odc;
                    }
                }
            }
            dst.a = tmpAlpha*minSide;
        }
    }
}
