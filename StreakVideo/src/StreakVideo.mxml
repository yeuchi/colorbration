<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   minWidth="955" minHeight="600"
			   creationComplete="onCreationComplete()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:states>
		<s:State name="default" enterState="onEnterDefault()"/>
		<s:State name="scan" enterState="onEnterScan()"/>
		<s:State name="count" enterState="onEnterCount()"/>			
	</s:states>
	
	<fx:Script>
		<![CDATA[
			import com.adobe.images.JPGEncoder;
			import com.adobe.images.PNGEncoder;
			import com.ctyeung.StreakCamera;
			import com.ctyeung.Targa.TGAEncoder;
			import com.ctyeung.WindowsBitmap.WinBmpEncoder;
			
			import flash.net.FileReference;
			
			import mx.controls.Alert;
			import mx.events.VideoEvent;
			import mx.graphics.codec.JPEGEncoder;

			private var fileReferenceList:FileReferenceList;
			protected static const BUTTON_WID:int = 100;
			protected static const PADDING:int = 10;
			protected static const DEFAULT_WID:int = 640;
			protected static const DEFAULT_LEN:int = 480;
			protected static const MODE_DEFAULT:String 	= "default";
			protected static const MODE_COUNT:String 	= "count";
			protected static const MODE_SCAN:String  	= "scan";
			protected var bmdTmp:BitmapData;
			protected var numFrames:uint;
			protected var count:uint;
			protected var camera:StreakCamera;
			protected var fileRef:FileReference;
			
			protected function onEnterScan():void {
				btnScan.enabled = true;
				btnCount.enabled = false;
				streakWidth.enabled = true;
			}
			
			protected function onEnterCount():void {
				btnScan.enabled = false;
				btnCount.enabled = true;
				streakWidth.enabled = false;
			}
			
			protected function onEnterDefault():void {
				btnScan.enabled = false;
				btnCount.enabled = false;
				streakWidth.enabled = false;
			}
			
			protected function onCreationComplete():void {
				fileRef = new FileReference();
				fileReferenceList = new FileReferenceList();
				fileReferenceList.addEventListener(Event.SELECT, selectHandler);
				fileReferenceList.addEventListener(IOErrorEvent.IO_ERROR, ioErrorHandler);
				fileReferenceList.addEventListener(ProgressEvent.PROGRESS, progressHandler);
				
				numFrames = 0;
				count = 0;
				this.addEventListener(Event.ENTER_FRAME, onEnterFrame, false, 0, true);
				video.addEventListener(VideoEvent.COMPLETE, onVideoComplete, false, 0, true);
				currentState = MODE_DEFAULT;
				camera = new StreakCamera();
			}
			
			protected function ioErrorHandler(e:Event):void {
				Alert.show("load Error"+e.toString());
			}
			
			protected function progressHandler(e:ProgressEvent):void {
				txtLoaded.text = (Number(e.bytesLoaded)/Number(e.bytesTotal)*100).toString()+"%";
			}
			
			private function selectHandler(event:Event):void {
				empty();
				this.count = 0;
				this.numFrames = 0;
				var fileReference:FileReference;
				var fileReferenceList:FileReferenceList = FileReferenceList(event.target);
				var fileList:Array = fileReferenceList.fileList;
				
				// get the first file that the user chose
				fileReference = FileReference(fileList[0]);
				
				// upload the file to the server side script
				fileReference.addEventListener(Event.COMPLETE, completeHandler);
				fileReference.upload(new URLRequest("http://www.ctyeung.com/flex/upload/upload.php"));
			}
			
			protected function completeHandler(event:Event):void {
				btnSave.enabled = false;
				video.autoPlay = false;
				this.currentState = MODE_COUNT;
				var ref:FileReference = event.currentTarget as FileReference;
				video.source = "http://www.ctyeung.com/flex/upload/"+ref.name;
				var w:int = video.videoObject.videoWidth;
				var h:int = video.videoObject.videoHeight;
				this.width = (w>DEFAULT_WID)?w:DEFAULT_WID + 100;
				this.height = (h>DEFAULT_LEN)?h: DEFAULT_LEN + 200;
			}
			
			protected function onBtnBrowse():void {
				var imageTypes:FileFilter = new FileFilter("Video", "*.f4v");
				fileReferenceList.browse([imageTypes]);
			}
			protected function onEnterFrame(e:Event):void {
				if(!video.playing)
					return;
				
				switch(currentState) {
					case MODE_SCAN:
						if(count<numFrames) {
							camera.scan(video, count);
						}
						else{
							video.stop();
						}
						++ count;
						break;
					
					case MODE_COUNT:
						++ numFrames;
						txtCount.text = "Frames:" + numFrames.toString();
						break;
				}
			}
			
			protected function onClickCount():void {
				numFrames = 0;
				count = 0;
				currentState = MODE_COUNT;
				video.stop();
				video.play();
			}
			
			protected function onClickScan():void {
				if(camera.bitmapData) {
					camera.streakWidth = this.streakWidth.value;
					camera.bitmapData.fillRect(camera.bitmapData.rect, 0xFFFFFF);
					count = 0;
					currentState = MODE_SCAN;
					video.stop();
					video.play();
				}
			}
			
			protected function empty():void {
				while(uic.numChildren) {
					var obj:Object = uic.getChildAt(0);
					uic.removeChildAt(0);
					if(obj is Bitmap) {
						(obj as Bitmap).bitmapData.dispose();
						obj = null;
					}
				}
				camera.dispose();
			}
			
			protected function onVideoComplete(e:Event):void {
				switch(currentState) {
					case MODE_SCAN:
						btnSave.enabled = true;
						break;
					
					case MODE_COUNT:
						currentState = MODE_SCAN;
						empty();
						camera.init(	video.videoObject.videoWidth, 
							video.videoObject.videoHeight,
							numFrames,
							streakWidth.value,
							StreakCamera.SCAN_HORIZONTAL);
						uic.addChild(new Bitmap(camera.bitmapData));
						uic.width = camera.scanLength/2;
						uic.height = video.videoObject.videoHeight/2;
						break;
				}
			}
			
			private function onBtnSave():void
			{
				var bytes:ByteArray = saveFile();
				fileRef.save(bytes, "panorama.png");
			}
			
			protected function saveFile():ByteArray
			{
				if (!camera.bitmapData) {
					Alert.show("Failed encoding");
					return null;
				}
				return PNGEncoder.encode(camera.bitmapData);
			}
		]]>
	</fx:Script>
	<s:VGroup horizontalAlign="left">
	<mx:HBox paddingBottom="{PADDING}" 
			 paddingLeft="{PADDING}" 
			 paddingRight="{PADDING}" 
			 paddingTop="{PADDING}" >
		<mx:Box id="playerBox" borderColor="0" borderStyle="solid">
			<s:VideoDisplay id="video" autoPlay="true" source="http://www.ctyeung.com/flex/upload/BlockBuster2.f4v" />
		</mx:Box>
		<mx:VBox>
			<s:Label id="txtLoaded" text="Video2Panorama"/>
			<mx:VBox borderColor="0x888888" 
					borderStyle="solid" 
					cornerRadius="5" 
					paddingBottom="{PADDING}" 
					paddingLeft="{PADDING}" 
					paddingRight="{PADDING}" 
					paddingTop="{PADDING}" >
				<mx:HBox verticalAlign="middle">
					<mx:Button id="btnLoad" width="{BUTTON_WID}" label="Upload" click="onBtnBrowse()"/>
					<mx:Spacer width="10"/>
					<s:Label text="Flash: *.f4v" width="{BUTTON_WID}"/>
				</mx:HBox>
				<mx:HBox verticalAlign="middle">
					<mx:Button id="btnSave" width="{BUTTON_WID}" label="Save" click="onBtnSave()" enabled="false"/>
					<mx:Spacer width="10"/>
					<s:Label text="Image: *.png" width="{BUTTON_WID}"/>
				</mx:HBox>
			</mx:VBox>
			<mx:Spacer height="5"/>
			<mx:HBox borderColor="0x888888" 
					 borderStyle="solid" 
					 cornerRadius="5" 
					 paddingBottom="{PADDING}" 
					 paddingLeft="{PADDING}" 
					 paddingRight="{PADDING}" 
					 paddingTop="{PADDING}" >
				<s:Button id="btnCount" width="{BUTTON_WID}" label="Count" click="onClickCount()"/>
				<mx:Spacer width="10"/>
				<s:Label id="txtCount" text="num frames=??" width="{BUTTON_WID}" />
			</mx:HBox>
			<mx:Spacer height="5"/>
			<mx:Box borderColor="0x888888" 
					borderStyle="solid" 
					cornerRadius="5" 
					paddingBottom="{PADDING}" 
					paddingLeft="{PADDING}" 
					paddingRight="{PADDING}" 
					paddingTop="{PADDING}" >
				
				<mx:HBox verticalAlign="middle">
					<s:NumericStepper id="streakWidth" width="{BUTTON_WID}" stepSize="2" value="6" minimum="2" maximum="30"/>
					<mx:Spacer width="10"/>
					<s:Label text="Slit width" width="{BUTTON_WID}"/>
				</mx:HBox>
				<s:Button id="btnScan" width="{BUTTON_WID}" label="Render" click="onClickScan()"/>
			</mx:Box>
		</mx:VBox>
	</mx:HBox>	
		<mx:Box borderColor="0" borderStyle="solid">
			<mx:UIComponent id="uic"/>
		</mx:Box>
		
	</s:VGroup>
</s:Application>
